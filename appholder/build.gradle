plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlinx.serialization.plugin
    alias libs.plugins.navigation.safe.args
    alias libs.plugins.parcelable
    alias libs.plugins.kapt
}

android {
    namespace "com.android.identity.wallet"
    compileSdk libs.versions.compile.sdk.get() as int

    defaultConfig {
        applicationId "com.android.identity.wallet"
        minSdkVersion libs.versions.min.sdk.get() as int
        targetSdkVersion libs.versions.compile.sdk.get() as int
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions.addAll("standard")
    productFlavors {
        wallet {
            dimension "standard"
        }
        purse {
            dimension "standard"
            applicationIdSuffix ".purse"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.kotlin.compiler.extension.get()
    }

    testOptions.unitTests.all {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'failed', 'skipped', 'standardOut', 'standardError'
        }
    }

    lint {
        lintConfig file('lint.xml')
    }
}

dependencies {
    // Uses module as identity credential api
    implementation project(':identity')
    implementation project(':identity-android')

    implementation platform(libs.compose.bom)
    implementation libs.bundles.androidx.core
    implementation libs.bundles.androidx.lifecycle
    implementation libs.bundles.androidx.navigation
    implementation libs.bundles.androidx.crypto
    implementation libs.bundles.bouncy.castle
    implementation libs.bundles.compose
    implementation libs.cbor
    implementation libs.exifinterface
    implementation libs.code.scanner
    implementation libs.kotlinx.serialization

    androidTestImplementation libs.bundles.ui.testing

    testImplementation libs.bundles.unit.testing

    testRuntimeOnly libs.junit.jupiter.engine
}